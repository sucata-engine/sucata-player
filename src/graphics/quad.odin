package graphics

import sg "../../sokol/gfx"
import "../camera"
import "../common"
import shader_quad "../shaders/quad"
import "core:c"

quad_ib: sg.Buffer
quad_buffers_inited: bool
quad_shader: sg.Shader
quad_pipeline: sg.Pipeline
quad_sampler: sg.Sampler

init_quad_indices :: proc() {
	if quad_buffers_inited {
		return
	}
	quad_shader = shader_quad.load_rect_shader()
	quad_pipeline = sg.make_pipeline(
		{
			shader = quad_shader,
			layout = {
				buffers = {0 = {stride = c.int(size_of(Vertex_Data))}},
				attrs = {
					shader_quad.ATTR_quad_position = {
						format = .FLOAT2,
						buffer_index = 0,
						offset = 0,
					},
					shader_quad.ATTR_quad_col = {format = .FLOAT4, buffer_index = 0, offset = 8},
					shader_quad.ATTR_quad_uv = {format = .FLOAT2, buffer_index = 0, offset = 24},
				},
			},
			index_type = .UINT16,
			colors = {
				0 = {
					blend = {
						enabled = true,
						src_factor_rgb = .SRC_ALPHA,
						dst_factor_rgb = .ONE_MINUS_SRC_ALPHA,
						src_factor_alpha = .ONE,
						dst_factor_alpha = .ONE_MINUS_SRC_ALPHA,
						op_rgb = .ADD,
						op_alpha = .ADD,
					},
				},
			},
		},
	)
	quad_ib = sg.make_buffer(
		{usage = {index_buffer = true, immutable = true}, data = sg_range(indices)},
	)

	quad_sampler = sg.make_sampler(
		{
			min_filter = .NEAREST,
			mag_filter = .NEAREST,
			wrap_u = .CLAMP_TO_EDGE,
			wrap_v = .CLAMP_TO_EDGE,
		},
	)

	quad_buffers_inited = true
}

shutdown_quad_buffers :: proc() {
	if quad_buffers_inited {
		sg.destroy_buffer(quad_ib)
		sg.destroy_pipeline(quad_pipeline)
		sg.destroy_sampler(quad_sampler)
		sg.destroy_shader(quad_shader)
		quad_buffers_inited = false
	}
}

quad :: proc(props: common.QuadObjectProps) {
	init_quad_indices()

	position := props.position
	size := props.size
	color := props.color
	z_index := props.zIndex
	texture := props.texture
	scale := props.scale
	origin := props.origin
	rotation := props.rotation
	atlas := props.atlas
	fixed := props.fixed
	shader_name := props.shader
	shader_args := props.shader_args

	image := load_image(texture)

	points := to_world_space_2d(position, size, scale, origin, rotation)

	opacity := props.opacity.(f32) or_else color[3]
	quad_color := Vec4{color[0], color[1], color[2], opacity}
	uv_pos := calculate_atlas_uv(atlas, f32(image.width), f32(image.height))

	vertices: [4]Vertex_Data
	vertices[0] = Vertex_Data {
		position = points[0],
		col      = quad_color,
		uv       = uv_pos[0],
	}
	vertices[1] = Vertex_Data {
		position = points[1],
		col      = quad_color,
		uv       = uv_pos[1],
	}
	vertices[2] = Vertex_Data {
		position = points[2],
		col      = quad_color,
		uv       = uv_pos[2],
	}
	vertices[3] = Vertex_Data {
		position = points[3],
		col      = quad_color,
		uv       = uv_pos[3],
	}

	mvp: matrix[4, 4]f32
	if props.fixed {
		mvp = get_fixed_mvp()
	} else {
		mvp = camera.get_view_projection_matrix(game_width, game_height)
	}

	vertex_buffers := [8]sg.Buffer{}
	has_vertex_buffer2 := false
	vertex_buffers[0] = sg.make_buffer(
		{
			usage = {vertex_buffer = true, immutable = true},
			size = uint(4 * size_of(Vertex_Data)),
			data = sg_range(vertices[:]),
		},
	)
	if shader_name != "" {
		if shader, ok := custom_shaders[shader_name]; ok {
			sg.apply_pipeline(shader.pipeline)

			shader_params := get_custom_shader_vertex_data(shader, shader_args)

			if len(shader_params) != 0 {
				vertex_buffers[1] = sg.make_buffer(
					{
						usage = {vertex_buffer = true, immutable = true},
						size = uint(4 * len(shader_params)),
						data = sg_range(shader_params[:]),
					},
				)
				has_vertex_buffer2 = true
			}
		} else {
			sg.apply_pipeline(quad_pipeline)
		}
	} else {
		sg.apply_pipeline(quad_pipeline)
	}
	sg.apply_uniforms(0, {ptr = &mvp, size = size_of(mvp)})

	quad_image := image.view
	bindings := sg.Bindings {
		vertex_buffers = vertex_buffers,
		index_buffer = quad_ib,
		views = {shader_quad.VIEW_tex = quad_image},
		samplers = {shader_quad.SMP_smp = quad_sampler},
	}

	sg.apply_bindings(bindings)
	sg.draw(0, 6, 1)

	sg.destroy_buffer(vertex_buffers[0])
	if has_vertex_buffer2 {
		sg.destroy_buffer(vertex_buffers[1])
	}
}
