package graphic

import common "../../common"
import core "../../core"
import lua_common "../lua_common"
import "core:c"
import lua "shared:luajit"

TEXT_FUNCTION :: lua_common.LuaFunction {
	name = "draw_text",
	func_ptr = proc "c" (L: ^lua.State) -> c.int {
		context = core.DEFAULT_CONTEXT

		if !lua_common.validate_arg_count(L, 1, "draw_text") do return 0
		if !lua_common.validate_table(L, 1, "draw_text") do return 0

		x := f32(lua_common.get_table_number(L, 1, "x", 0.0))
		y := f32(lua_common.get_table_number(L, 1, "y", 0.0))
		font_size := f32(lua_common.get_table_number(L, 1, "size", 16.0))
		color := lua_common.get_table_string(L, 1, "color", "#ffffff")
		zIndex := lua_common.get_table_number(L, 1, "z_index", 0)
		text := lua_common.get_table_string(L, 1, "text", "")
		font := lua_common.get_table_string(L, 1, "font", "")
		scale := f32(lua_common.get_table_number(L, 1, "scale", 1.0))
		scale_x := f32(lua_common.get_table_number(L, 1, "scale_x", 1.0))
		scale_y := f32(lua_common.get_table_number(L, 1, "scale_y", 1.0))
		origin := f32(lua_common.get_table_number(L, 1, "origin", 0.0))
		origin_x := f32(lua_common.get_table_number(L, 1, "origin_x", 0.0))
		origin_y := f32(lua_common.get_table_number(L, 1, "origin_y", 0.0))
		rotation := f32(lua_common.get_table_number(L, 1, "rotation", 0.0))
		fixed := lua_common.get_table_boolean(L, 1, "fixed", false)
		align := lua_common.get_table_string(L, 1, "align", "left")
		max_width := f32(lua_common.get_table_number(L, 1, "max_width", 0.0))
		opacity := lua_common.get_table_number_nil(L, 1, "opacity")
		shader := lua_common.get_table_string(L, 1, "shader", "")
		shader_args := lua_common.get_shader_args(L, 1)

		if scale != 1.0 && (scale_x == 1.0 && scale_y == 1.0) {
			scale_x = scale
			scale_y = scale
		}

		if origin != 0.0 && (origin_x == 0.0 && origin_y == 0.0) {
			origin_x = origin
			origin_y = origin
		}

		text_align := common.TextAlign.Left
		defer delete(align)
		switch align {
		case "center":
			text_align = .Center
		case "right":
			text_align = .Right
		case "left":
			text_align = .Left
		}

		color_rgba := hex_to_rgba(color)
		defer delete(color)

		props := common.TextObjectProps {
			position    = [2]f32{x, y},
			color       = color_rgba,
			zIndex      = i32(zIndex),
			font        = font,
			size        = font_size,
			shader      = shader,
			scale       = [2]f32{scale_x, scale_y},
			origin      = [2]f32{origin_x, origin_y},
			rotation    = rotation,
			opacity     = opacity,
			text        = text,
			fixed       = fixed,
			align       = text_align,
			maxWidth    = max_width,
			shader_args = shader_args,
		}

		core.add_to_render_queue(props)

		return 0
	},
}
