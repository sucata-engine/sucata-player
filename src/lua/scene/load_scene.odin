package scene

import common "../../common"
import core "../../core"
import lua_common "../lua_common"
import "core:c"
import lua "shared:lua55"

LOAD_SCENE_FUNCTION :: lua_common.LuaFunction {
	name = "load_scene",
	func_ptr = proc "c" (L: ^lua.State) -> c.int {
		context = core.DEFAULT_CONTEXT

		if !lua_common.validate_arg_count(L, 1, "load_scene") do return 0
		if !lua_common.validate_table(L, 1, "load_scene") do return 0

		entities := [dynamic]^common.Entity{}

		table_length := c.int(lua.rawlen(L, 1))

		for i in 1 ..= table_length {
			lua.rawgeti(L, 1, lua.Integer(i))
			if lua.istable(L, -1) {
				entity := lua_common.create_entity_by_lua(L, c.int(lua.gettop(L)))
				append(&entities, entity)
			}
			lua.pop(L, 1)
		}

		core.load_scene(entities)

		return 0
	},
}
