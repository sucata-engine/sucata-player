package core

import camera "../camera"
import "core:math"
import "core:sort"
import "core:strings"

Hoverable :: struct {
	id:      string,
	x:       f32,
	y:       f32,
	width:   f32,
	height:  f32,
	z_index: i32,
	fixed:   bool,
}

hoverables: [dynamic]Hoverable
hover := ""

add_hoverable :: proc(id: string, x, y, width, height: f32, z_index: i32, fixed: bool) {
	id_clone := strings.clone(id)
	hoverable := Hoverable {
		id      = id_clone,
		x       = x,
		y       = y,
		width   = width,
		height  = height,
		z_index = z_index,
		fixed   = fixed,
	}
	append_elem(&hoverables, hoverable)
}

set_hoverable :: proc(id: string) {
	hover = strings.clone(id)
}

process_hoverables :: proc() {
	if len(hoverables) <= 0 {
		return
	}
	if hover != "" {
		delete(hover)
		hover = ""
	}

	_hoverables := hoverables[:]
	sort.quick_sort_proc(_hoverables, proc(a, b: Hoverable) -> int {
		if a.z_index > b.z_index {
			return -1
		} else if a.z_index < b.z_index {
			return 1
		}
		return 0
	})

	mouse_x, mouse_y := mouse_position()

	if camera.camera.rotation != 0 {
		cos_r := math.cos(-camera.camera.rotation)
		sin_r := math.sin(-camera.camera.rotation)
		rotated_x := mouse_x * cos_r - mouse_y * sin_r
		rotated_y := mouse_x * sin_r + mouse_y * cos_r
		mouse_x = rotated_x
		mouse_y = rotated_y
	}

	world_mouse_x := (mouse_x + camera.camera.position.x / camera.camera.zoom)
	world_mouse_y := (mouse_y + camera.camera.position.y / camera.camera.zoom)

	for hoverable in _hoverables {
		if hoverable.fixed {
			if mouse_x >= hoverable.x &&
			   mouse_x <= hoverable.x + hoverable.width &&
			   mouse_y >= hoverable.y &&
			   mouse_y <= hoverable.y + hoverable.height {
				set_hoverable(hoverable.id)
				break
			}
		} else {
			if world_mouse_x >= hoverable.x &&
			   world_mouse_x <= hoverable.x + hoverable.width &&
			   world_mouse_y >= hoverable.y &&
			   world_mouse_y <= hoverable.y + hoverable.height {
				set_hoverable(hoverable.id)
				break
			}
		}
	}

	clear_hoverables()
}

clear_hoverables :: proc() {
	for hoverable in hoverables {
		delete(hoverable.id)
	}
	clear(&hoverables)
}
